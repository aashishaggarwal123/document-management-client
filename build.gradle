buildscript {
  ext {
    springBootVersion = '1.5.8.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

apply plugin: 'checkstyle'
apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply 'maven'
apply 'maven-publish'
apply "com.jfrog.bintray" version "1.7.3"

group 'uk.gov.hmcts.reform.cmc'
version '1.0.0'

checkstyle.toolVersion = '7.2'
checkstyle.configFile = new File(rootDir, "checkstyle.xml")

tasks.withType(Checkstyle).each { checkstyleTask ->
  checkstyleTask.doLast {
    reports.all { report ->
      def outputFile = report.destination
      if (outputFile.exists() && outputFile.text.contains("<error ")) {
        throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
      }
    }
  }
}

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}


jar {
  manifest {
    attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
  }
}

task printVersion {
  doLast {
    print project.version
  }
}


def pomConfig = {
  licenses {
    license {
      name "MIT License"
      url "http://www.opensource.org/licenses/mit-license.php"
      distribution "repo"
    }
  }

  scm {
    url "https://github.com/hmcts/document-management-client"
  }
}


task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    Main(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId project.group
      artifactId 'document-management-client'
      version project.version

      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Client library for communicating with document management gateway')
        root.appendNode('name', 'CMC document management client')
        root.appendNode('url', 'https://github.com/hmcts/document-management-client')
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['Main']
  publish = true
  pkg {
    repo = 'hmcts-maven'
    name = 'document-management-client'
    userOrg = 'hmcts'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/hmcts/document-management-client'
    version {
      name = project.version
    }
  }
}

ext {
  springCloudVersion = 'Dalston.SR4'
}

dependencies {
  compile('org.springframework.cloud:spring-cloud-starter-feign')

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.testcontainers:testcontainers:1.4.3')
  testCompile('com.github.tomakehurst:wiremock:2.10.1')
  testCompile('org.eclipse.jetty:jetty-server')
  testCompile('org.eclipse.jetty:jetty-servlet')
  testCompile('org.eclipse.jetty:jetty-servlets')
  testCompile('org.apache.httpcomponents:httpclient')
  testCompile('com.netflix.feign:feign-jackson:8.18.0')
  testCompile('io.github.openfeign.form:feign-form-spring:2.0.5')
  testCompile('org.assertj:assertj-core:3.8.0')

}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

bootRepackage {
  enabled = false;
}
